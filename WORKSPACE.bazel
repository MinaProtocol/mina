workspace( name = "mina" )
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
load("@bazel_tools//tools/build_defs/repo:git.bzl", "git_repository", "new_git_repository")

## Repository Bootstrapping: Fetch, Load, and Run

## For each language, we need to fetch the language rules repos,
## load function symbols, and then execute the functions.
## Then we need to do the same for our repo deps,
## and where our direct repo deps have their own repo deps,
## we need load and run the functions that bootstrap them.

## We encapsulate the rule fetches (repository rules) in extension
## functions called "<lang>_fetch_rules"; these are found in
## "//:WORKSPACE.bzl".

## For library repos we encapsulate fetches in functions called
## "*_fetch_repo" (or "_repos", or "_libs"). For direct depdendents
## these are located in "bzl/external/<lang>_repos.bzl"; for example
## see "bzl/external/ocaml_repos.bzl". For indirect dependents -
## repositories that the direct dependents import - they are located
## in extension files within the repo, usually "//:BUILD.bzl". See
## libp2p below for an example.

## However we cannot encapsulate "load(...)" statements, like this,
## since they must be at the top level. So our procedure is to call
## the functions that fetch all the rules/repos at one go, and then
## issue "load" statements to get access to the functions, and then
## run the loaded functions.

## IMPORTANT: order matters.

################ C/C++ Rules ################
## these are needed by some external repos, like zlib
load("//:WORKSPACE.bzl", "cc_fetch_rules")
# load("//bzl/external:cc_repos.bzl", "cc_fetch_rules")
cc_fetch_rules()

load("@rules_foreign_cc//:workspace_definitions.bzl", "rules_foreign_cc_dependencies")
rules_foreign_cc_dependencies()

################################################
################ Golang Support ################
## libp2p_helper is a repository, but is embedded, not remote, so we
## use "local_repository", which must be executed as a top-level
## instruction in the workspace file.
local_repository(name = "libp2p_helper", path = "src/app/libp2p_helper")

## since libp2p_helper is our only golang repo, it includes the
## language fetch rules as well as its indirect lib fetch rules.
# gazelle:proto disable_global
load("@libp2p_helper//:WORKSPACE.bzl", "libp2p_fetch_go_rules", "libp2p_fetch_go_libs")

# first we fetch the repos containing the go language rules.
libp2p_fetch_go_rules()

# now we can load the function symbols we need from the fetched language rules.
load("@io_bazel_rules_go//go:deps.bzl",
     "go_download_sdk",
     "go_register_toolchains",
     "go_rules_dependencies",
     )

# finally, we execute the functions, which initialize language support.
go_download_sdk(
    name = "go_sdk",
    version = "1.13",
)
go_rules_dependencies()
go_register_toolchains()

# we don't use protobufs, but the language rules do, so we need this:
load("@com_google_protobuf//:protobuf_deps.bzl", "protobuf_deps")
protobuf_deps()

# gazelle is a tool we use to maintain our go build code.
load("@bazel_gazelle//:deps.bzl", "gazelle_dependencies", "go_repository")

# once the language support is in place, we fetch the lib repos our code depends on.
libp2p_fetch_go_libs(go_repository)

# and prep the gazelle tool.
gazelle_dependencies(go_repository_default_config = "@libp2p_helper//:WORKSPACE.bazel")

######################################
###############  OCaml ###############
# now we do the same for the OCaml rules and lib repos.
load("//:WORKSPACE.bzl", "ocaml_fetch_rules")
ocaml_fetch_rules()

load("@bazel_skylib//:workspace.bzl", "bazel_skylib_workspace")
bazel_skylib_workspace()

load("@obazl_rules_opam//opam:bootstrap.bzl", opam_configure = "configure")
load("//bzl:opam.bzl", "opam")
switch = opam_configure(opam = opam)

load("@obazl_rules_ocaml//ocaml:bootstrap.bzl", ocaml_configure = "configure")
ocaml_configure( switch = switch )

# direct deps
load("//bzl/external:ocaml_repos.bzl", "ocaml_fetch_libs")
ocaml_fetch_libs()

# indirect deps - these all depend on CC libs, which must be fetched
load("@ocaml_rocksdb//:WORKSPACE.bzl", "librocksdb_fetch_repos")
librocksdb_fetch_repos()

load("@ocaml_sodium//:WORKSPACE.bzl", "libsodium_fetch_repo")
libsodium_fetch_repo("1.0.18-RELEASE")

load("@ocaml_jemalloc//:WORKSPACE.bzl", "libjemalloc_fetch_repo")
libjemalloc_fetch_repo()

################################################
################  RUST SUPPORT  ################
## we have more than one repo using Rust, so we put the fetch rules in
## this repo for both to use.
load("//:WORKSPACE.bzl", "rust_fetch_rules")
# load("//bzl/external:rust_repos.bzl", "rust_fetch_rules")
rust_fetch_rules()

load("@io_bazel_rules_rust//rust:repositories.bzl", "rust_repositories")
rust_repositories(version = "1.47.0", edition="2018")

load("@io_bazel_rules_rust//:workspace.bzl", "bazel_version")
bazel_version(name = "bazel_version")

#### direct Rust dependencies ####
load("//bzl/external:rust_repos.bzl", "rust_fetch_libs")
rust_fetch_libs()

#### indirect Rust dependencies ####
load("@zexe//bzl/cargo:crates.bzl", "zexe_fetch_remote_crates")
zexe_fetch_remote_crates()

load("@marlin//bzl/cargo:crates.bzl", marlin_fetch_remote_crates = "raze_fetch_remote_crates")
marlin_fetch_remote_crates()

## local rust code, configured by "cargo raze"
load("//bzl/external/rust:crates.bzl", "marlin_plonk_bindings_fetch_remote_crates")
marlin_plonk_bindings_fetch_remote_crates()
